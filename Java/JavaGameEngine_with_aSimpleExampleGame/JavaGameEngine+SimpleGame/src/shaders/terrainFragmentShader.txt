#version 400 core

in vec2 pass_textureCoords; //flat
in vec3 surfaceNormal; //flat
in vec3 toLightVector[4];
in vec3 toCameraVector;
in float visibility;

out vec4 outColor;

uniform sampler2D backgroundTexture;
uniform sampler2D rTexture;
uniform sampler2D gTexture;
uniform sampler2D bTexture;
uniform sampler2D blendMap;

uniform vec3 lightColor[4];
uniform vec3 attenuation[4];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor;

void main(void) {

	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitCameraVector = normalize(toCameraVector);

	vec3 totalDifuseTerrain = vec3(0.0);
	vec3 totalSpecularTerrain = vec3(0.0);

	for(int i=0;i<4;i++){
		float distance = length(toLightVector[i]);
		float attFactor = attenuation[i].x + (attenuation[i].y * distance) + (attenuation[i].z * distance * distance);
		vec3 unitLightVector = normalize(toLightVector[i]);		
		float nDot1 = dot(unitNormal, unitLightVector);
		float brightness = max(nDot1,0.0);
		vec3 fromLightVector = -unitLightVector;
		vec3 reflectedVector = reflect(fromLightVector, unitNormal);
		float specularFactor = dot(reflectedVector, unitCameraVector);
		specularFactor = max(specularFactor, 0.0); 
		float dampedFactor = pow(specularFactor, shineDamper);
		totalDifuseTerrain = totalDifuseTerrain + (brightness * lightColor[i])/attFactor;;
		totalSpecularTerrain = totalSpecularTerrain + (dampedFactor * reflectivity *  lightColor[i])/attFactor;
		
	}
	totalDifuseTerrain = max(totalDifuseTerrain, 0.2);
	
	vec4 blendMapColor = texture(blendMap, pass_textureCoords);
	float backTextureAmount = 1 - (blendMapColor.r + blendMapColor.g + blendMapColor.b);
	vec2 tiledCoords = pass_textureCoords * 40.0;
	vec4 backgroundTextureColor = texture(backgroundTexture, tiledCoords) * backTextureAmount;
	vec4 rTextureColor = texture(rTexture, tiledCoords) * blendMapColor.r;
	vec4 gTextureColor = texture(gTexture, tiledCoords) * blendMapColor.g;
	vec4 bTextureColor = texture(bTexture, tiledCoords) * blendMapColor.b;
	vec4 totalColor = backgroundTextureColor + rTextureColor + gTextureColor + bTextureColor;

	outColor = (vec4(totalDifuseTerrain, 1.0) * totalColor + vec4(totalSpecularTerrain, 1.0)); 
	//vec4(diffuse, 1.0) * texture(textureSampler, pass_textureCoords) + vec4(finalSpecular, 1.0)
	outColor = mix(vec4(skyColor, 1.0), outColor, visibility);

}